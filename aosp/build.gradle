buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.+'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'android-maven'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.0"

    sourceSets {
        main {
            manifest {
                srcFile 'AndroidManifest.xml'
            }
            java {
                srcDir 'src'
            }
            res {
                srcDir 'res'
            }
            assets {
                srcDir 'assets'
            }
            resources {
                srcDir 'src'
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName "0.1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


configure(install.repositories.mavenInstaller) {
    pom.project {
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'JordanRobinson'
                name 'Jordan Robinson'
                email 'me@jordanrobinson.co.uk'
            }
        }
        scm {
            connection 'git@github.com:jordanrobinson/android-color-picker.git'
            developerConnection 'http://jordanrobinson.co.uk'
            url 'http://github.com/jordanrobinson/android-color-picker'
        }
    }
}

install {
    repositories.mavenInstaller {
      // This generates POM.xml with proper parameters
      pom {
        project {
            packaging 'aar'

                // Add your description here
                name 'Android Color Picker'
                url 'https://github.com/jordanrobinson/android-color-picker'
            }
        }
    }
}

dependencies {
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "Android Color Picker"
        websiteUrl = 'https://github.com/jordanrobinson/android-color-picker'
        vcsUrl = 'git@github.com:jordanrobinson/android-color-picker.git'
        licenses = ["MIT"]
        publish = true
    }
}

